## Protocol

Vader

## Issue

In `VaderPoolV2::mintSynth`, a user sends `nativeDeposit` to the pool and mints equivalent amount of `foreignAsset` as a synth and sent back to user. However, note that a user cannot submit a `min output` amount of synth that they would accept.

A malicious user can manipulate the reserves of the pool inorder to make `foreignAsset` more valuable for user. As a result, user ends up with a lesser amount of `foreignAsset` synth than he should. In this case, a front runner first buys `foreignAsset` before the user does & immediately sells that `foreignAsset` once user synth is minted. Since the price after user txn is higher than purchase price, frontrunner ends up with a profit in this attack (also called the sandwich attack)

Burning synths effectively has a similar effect but in the opposite direction.

```solidity
  function mintSynth(
        IERC20 foreignAsset,
        uint256 nativeDeposit,
        address from,
        address to
    )
        external
        override
        nonReentrant
        supportedToken(foreignAsset)
        returns (uint256 amountSynth)
    {
        nativeAsset.safeTransferFrom(from, address(this), nativeDeposit);

        ISynth synth = synthFactory.synths(foreignAsset);

        if (synth == ISynth(_ZERO_ADDRESS))
            synth = synthFactory.createSynth(
                IERC20Extended(address(foreignAsset))
            );

        (uint112 reserveNative, uint112 reserveForeign, ) = getReserves(
            foreignAsset
        ); // gas savings

        amountSynth = VaderMath.calculateSwap(
            nativeDeposit,
            reserveNative,
            reserveForeign
        ); //@audit note that there is no minimum amount while swapping the native deposit to the synth

        // TODO: Clarify
        _update(
            foreignAsset,
            reserveNative + nativeDeposit,
            reserveForeign,
            reserveNative,
            reserveForeign
        );

        synth.mint(to, amountSynth);
    }

```

## Impact

Amount of synths minted/assets received when minting can be manipulated

## Code

https://github.com/code-423n4/2021-11-vader/blob/607d2b9e253d59c782e921bfc2951184d3f65825/contracts/dex-v2/pool/VaderPoolV2.sol#L126-L167

## Recommendation

Consider passing a minimum amount for execution of swap

## Links

https://code4rena.com/reports/2021-11-vader/#h-31-unused-slippage-params

## Learning

Concept here is the same -> when you are exchanging one asset from another via a pool that is accessible to others, there is every chance that other users can execute a sandwich attack on you if you don't mention a minimum execution price
