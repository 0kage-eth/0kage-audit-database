## Protocol

AbraNFT

## Issue

We saw a similar issue with [0003.md](./0003.md) where lender could change oracle when loan becomes outstanding. In this case, borrower can construct a loan with an arbitrary oracle. A malicious borrower can setup an oracle whose price never exceeds the liquidation threshold -> till liquidation threshold, oracle can exactly mimic a genuine oracle & once the price breaches liquidation threshold, malicious oracle can place a hard cap on price.

## Impact

Borrower can bypass liquidations completely.

The `removeCollateral` function does not allow liquidation

```solidity
    //NFTPairWithOracle::removeCollateral

    function removeCollateral(uint256 tokenId, address to) public {
        TokenLoan memory loan = tokenLoan[tokenId];
        if (loan.status == LOAN_REQUESTED) {
            // We are withdrawing collateral that is not in use:
            require(msg.sender == loan.borrower, "NFTPair: not the borrower");
        } else if (loan.status == LOAN_OUTSTANDING) {
            // We are seizing collateral towards the lender. The loan has to be
            // expired and not paid off, or underwater and not paid off:
            require(to == loan.lender, "NFTPair: not the lender");

            if (uint256(loan.startTime) + tokenLoanParams[tokenId].duration > block.timestamp) {
                TokenLoanParams memory loanParams = tokenLoanParams[tokenId];
                // No underflow: loan.startTime is only ever set to a block timestamp
                // Cast is safe: if this overflows, then all loans have expired anyway
                uint256 interest = calculateInterest(
                    loanParams.valuation,
                    uint64(block.timestamp - loan.startTime),
                    loanParams.annualInterestBPS
                ).to128();
                uint256 amount = loanParams.valuation + interest;
                (, uint256 rate) = loanParams.oracle.get(address(this), tokenId);
                require(rate.mul(loanParams.ltvBPS) / BPS < amount, "NFT is still valued"); //@audit malicious oracle can make this revert always
            }
        }
```

## Code

https://github.com/debtdao/Line-of-Credit/blob/e8aa08b44f6132a5ed901f8daa231700c5afeb3a/contracts/modules/credit/LineOfCredit.sol#L84

## Recommendation

- In the `_lend` function, consider adding a check for collateral

```
    function _lend(
        address lender,
        uint256 tokenId,
        TokenLoanParams memory accepted,
        bool skim
    ) internal {
        TokenLoan memory loan = tokenLoan[tokenId];
        require(loan.status == LOAN_REQUESTED, "NFTPair: not available");
        TokenLoanParams memory params = tokenLoanParams[tokenId];

        // Valuation has to be an exact match, everything else must be at least
        // as good for the lender as `accepted`.
        require(
            params.valuation == accepted.valuation &&
                params.duration <= accepted.duration &&
                params.annualInterestBPS >= accepted.annualInterestBPS &&
                params.ltvBPS >= accepted.ltvBPS,
            "NFTPair: bad params"
        ); //@audit -> add a check if oracle is an accepted oracle
        //...
    }
```

## Links

C-4 final report submission here:
https://code4rena.com/reports/2022-04-abranft/#h-01-avoidance-of-liquidation-via-malicious-oracle

## Learning

In this particular case, terms made by borrower are verified by lender - while all critical terms such as duration, rate, LTV and principal are verified, the devs forgot to check for oracle. When it comes to collateral management, checking validity of oracles is extremely important as oracles control the liquidation trigger
