## Protocol

Cooler

## Issue

In the following example, placement of `delete` before repayment leads to a scenario where a user who makes a full repayment will have his funds send to 0 address. Although repaid, lender will never get these funds & face permanent loss.

Note how a full repayment will delete `loan` struct & when repayment is done for `loan.lender`, funds go to 0 address and the loan no longer exists.

```
function repay (uint256 loanID, uint256 repaid) external {
    Loan storage loan = loans[loanID];

    if (block.timestamp > loan.expiry)
        revert Default();

    uint256 decollateralized = loan.collateral * repaid / loan.amount;

    // @audit loans[loanID] is deleted here
    // which means that loan which points to loans[loanID]
    // will be an empty object with default/0 member values
    if (repaid == loan.amount) delete loans[loanID];
    else {
        loan.amount -= repaid;
        loan.collateral -= decollateralized;
    }

    // @audit loan.lender = 0 due to the above delete
    // hence repayment will be sent to the zero address
    // some erc20 tokens will revert but many will happily
    // execute and the repayment will be lost forever
    debt.transferFrom(msg.sender, loan.lender, repaid);
    collateral.transfer(owner, decollateralized);
}
```

## Impact

Loan gets closed without lender ever receiving repayment

## Code

https://github.com/sherlock-audit/2023-01-cooler/blob/main/src/Cooler.sol#L108-L124

## Recommendation

Store lender address in a temporary variable & yuse that address for transfers

## Links

https://github.com/sherlock-audit/2023-01-cooler-judging/issues/33

## Learning

In this case, devs simply assumed that a storage variable persists forever - there is a blindspot here where a dev ignored a case when variable is deleted as part of prior logic.

When deleting storage variables, it is important to question any usage of that variable in subsequent logic - is there a scenario when the variable can be reused assuming it persists
