## Protocol

Timeswap

## Issue

User can call `pay` with `payParams` that are listed below

```
    /// @param maturity The unix timestamp maturity of the Pool.
    /// @param to The address of the receiver of collateral ERC20.
    /// @param owner The addres of the owner of collateralized debt.
    /// @param ids The array indexes of collateralized debts.
    /// @param assetsIn The amount of asset ERC20 paid per collateralized debts.
    /// @param collateralsOut The amount of collateral ERC20 withdrawn per collaterlaized debts.
    /// @param data The data for callback.
    struct PayParam {
        uint256 maturity;
        address to;
        address owner;
        uint256[] ids;
        uint112[] assetsIn;
        uint112[] collateralsOut;
        bytes data;
    }
```

Note that a user can pass an array of `assetsIn`, ie. the amounts to be repaid per ERC20 token, and `collateralsOut`, ie. the amount of collateral to be withdrawn on repayment of assets.

The pay function is listed below

```solidity
function pay(PayParam calldata param)
    external
    override
    lock
    returns (
        uint128 assetIn,
        uint128 collateralOut
    )
{
    require(block.timestamp < param.maturity, 'E202');
    require(param.owner != address(0), 'E201');
    require(param.to != address(0), 'E201');
    require(param.to != address(this), 'E204');
    require(param.ids.length == param.assetsIn.length, 'E205');
    require(param.ids.length == param.collateralsOut.length, 'E205');
    Pool storage pool = pools[param.maturity];
    Due[] storage dues = pool.dues[param.owner];
    require(dues.length >= param.ids.length, 'E205');
    for (uint256 i; i < param.ids.length;) {
        Due storage due = dues[param.ids[i]];
        require(due.startBlock != BlockNumber.get(), 'E207');
        if (param.owner != msg.sender) require(param.collateralsOut[i] == 0, 'E213');
        require(uint256(assetIn) * due.collateral >= uint256(collateralOut) * due.debt, 'E303'); //@audit -> note that for the first time this loop is run, assetIn and collateralOut are both 0, since they are not used thus far
        due.debt -= param.assetsIn[i];
        due.collateral -= param.collateralsOut[i];
        assetIn += param.assetsIn[i];
        collateralOut += param.collateralsOut[i];
        unchecked { ++i; }
    }
    ...
```

Note the condition that checks that ratio of `assetIn/collateralOut` should be greater than or equal to `debt/collateral` is called repeatedly inside a loop. Post this condition being true, `assetIn` and `collateralOut` are updated. For the first run of this loop, note that `assetIn` and `collateralOut` == 0.

So the condition

```
     require(uint256(assetIn) * due.collateral >= uint256(collateralOut) * due.debt, 'E303');
```

So, if we pass an array of single size in pay params, ie. `assetsIn[0]=0`, `collateralsOut[0]=0`, then above condition is true. Subsequently `collateralOut` is paid back to the caller - so even though debt accounting entry for user stays the same, collateral is completely withdrawn by the user. There is no incentive for user to pay back his debt anymore.

## Impact

Borrower gets back his entire collateral without repaying loan.

## Code

https://github.com/code-423n4/2022-03-timeswap/blob/00317d9a8319715a8e28361901ab14fe50d06172/Timeswap/Core/contracts/TimeswapPair.sol#L459-L490

## Recommendation

The condition should be outside of the loop & not inside it, ie. condition should be checked once all assets are assumed to be repaid and all collateral is assumed to be drawn out. If condition does not satisfy, we basically revert.

```
for (uint256 i; i < param.ids.length;) {
    Due storage due = dues[param.ids[i]];
    require(due.startBlock != BlockNumber.get(), 'E207');
    if (param.owner != msg.sender) require(param.collateralsOut[i] == 0, 'E213');
    due.debt -= param.assetsIn[i];
    due.collateral -= param.collateralsOut[i];
    assetIn += param.assetsIn[i];
    collateralOut += param.collateralsOut[i];
    unchecked { ++i; }
}
require(uint256(assetIn) * due.collateral >= uint256(collateralOut) * due.debt, 'E303');
...
```

## Links

https://code4rena.com/reports/2022-03-timeswap/#h-01-wrong-timing-of-check-allows-users-to-withdraw-collateral-without-paying-for-the-debt

## Learning

This is a very insightful finding -> by misplacing a condition & checking condition BEFORE values are updated, protocol here has created a critical vulnerability.

When conditions are inside a loop -> it is a good practice to check the first and last iterations -> first iteration generally will use state/local variables for first time & each iteration can update the values further.
