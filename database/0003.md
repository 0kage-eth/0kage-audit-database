## Protocol

Abra NFT

## Issue

Issue here is that lender can change oracle that determines the price of collateral. This allows a malicious lender to update oracle that can artificially reduce value of collateral so that lender can take control of borrowers collateral via liquidation.

Once a loan is outstanding, lender can call `updateLoanParams` and pass its own params including the oracle address. In a scenario where value of collateral has increased a lot, much more than outstanding + interest, a malicious lender can pass his own oracle after loan becomes outstanding. There is no check to ensure collateral is a valid address

## Impact

Borrower stands to lose entire collateral once the oracle is manipulated. Lender gets the collateral for a pittance.

## Code

https://github.com/code-423n4/2022-04-abranft/blob/5cd4edc3298c05748e952f8a8c93e42f930a78c2/contracts/NFTPairWithOracle.sol#L200-L211

https://github.com/code-423n4/2022-04-abranft/blob/5cd4edc3298c05748e952f8a8c93e42f930a78c2/contracts/NFTPairWithOracle.sol#L286-L288

```
solidity
NFTPairWithOracle updateLoanParams

    //@audit note that lender can change the params if loan status is outstanding
    // also note that there is no check that current loan oracle is same as the updated loan oracle
    function updateLoanParams(uint256 tokenId, TokenLoanParams memory params) public {
        TokenLoan memory loan = tokenLoan[tokenId];
        if (loan.status == LOAN_OUTSTANDING) {
            // The lender can change terms so long as the changes are strictly
            // the same or better for the borrower:
            require(msg.sender == loan.lender, "NFTPair: not the lender");
            TokenLoanParams memory cur = tokenLoanParams[tokenId];
            require(
                params.duration >= cur.duration &&
                    params.valuation <= cur.valuation &&
                    params.annualInterestBPS <= cur.annualInterestBPS &&
                    params.ltvBPS <= cur.ltvBPS,
                "NFTPair: worse params"
            );
        }

```

```
solidity
NFTPairWithOracle removeCollateral
    //@audit a manipulated collateral can then be used to remove collateral
    // and lender takes control of that collateral
     function removeCollateral(uint256 tokenId, address to) public {
        TokenLoan memory loan = tokenLoan[tokenId];
        if (loan.status == LOAN_REQUESTED) {
            // We are withdrawing collateral that is not in use:
            require(msg.sender == loan.borrower, "NFTPair: not the borrower");
        } else if (loan.status == LOAN_OUTSTANDING) {
            // We are seizing collateral towards the lender. The loan has to be
            // expired and not paid off, or underwater and not paid off:
            require(to == loan.lender, "NFTPair: not the lender");

            if (uint256(loan.startTime) + tokenLoanParams[tokenId].duration > block.timestamp) {
                TokenLoanParams memory loanParams = tokenLoanParams[tokenId];
                // No underflow: loan.startTime is only ever set to a block timestamp
                // Cast is safe: if this overflows, then all loans have expired anyway
                uint256 interest = calculateInterest(
                    loanParams.valuation,
                    uint64(block.timestamp - loan.startTime),
                    loanParams.annualInterestBPS
                ).to128();
                uint256 amount = loanParams.valuation + interest;
                (, uint256 rate) = loanParams.oracle.get(address(this), tokenId); //@audit this uses oracle of loanParams to calculate the rate
                require(rate.mul(loanParams.ltvBPS) / BPS < amount, "NFT is still valued"); //@audit that rate is used to check collateral value
            }
        }
        // If there somehow is collateral but no accompanying loan, then anyone
        // can claim it by first requesting a loan with `skim` set to true, and
        // then withdrawing. So we might as well allow it here..
        delete tokenLoan[tokenId];
        collateral.transferFrom(address(this), to, tokenId);
        emit LogRemoveCollateral(tokenId, to);
    }

```

## Recommendation

Disallow lender from updating collateral -> when updating loan params, make sure collateral sanctity check is introduced

## Links

Read full finding on C4 report here

https://code4rena.com/reports/2022-04-abranft/#h-03-critical-oracle-manipulation-risk-by-lender
