## Protocol

Taurus

## Issue

In this protocol, role of a `keeper` is to perform liquidations and to swap yield token for TAU using the `SwapHandler.swapForTau` function. Keeper is able to choose how much yield token to swap and what the proportion of the resulting TAU is that is distributed to users vs. not distributed in order to erase bad debt. Proportion distributed to users is a way to pay back user loans - percentage withheld is simply burned and has a net effect of erasing bad debt in the vault.

However, there is no validation on a key input to `SwapHandler.swapForTau` function, which is `_rewardProportion`. Since there is no cap on the `_rewardProportion`, a high value will not just erase bad debts but will erase all debt. So users can withdraw their collateral without paying any of the debt. A keeper can escalate their permissions in a malicious way..

```
   /**
     * @dev function called as part of the yield pull process. This will fetch swap modules from the Controller, use them
        to handle a swap from vault yield to tau, then validate that the swap did not encounter too much slippage.
     * @param _yieldTokenAddress is the address of the token to be swapped. Must be a yield token, so cannot be the vault's collateral token or tau.
     * @param _yieldTokenAmount is the amount of yield token. Some will be transferred to the FeeSplitter for use by the protocol. The rest will be swapped for tau.
     * note that slippage parameters must be built based on the amount to be swapped, not based on _yieldTokenAmount above (some of which will not be swapped).
     * @param _swapAdapterHash is the hash of the swap adapter to be used, i.e. keccak256("UniswapSwapAdapter") for the UniswapSwapAdapter.
     * @param _rewardProportion refers to the proportion of received tau which will be rewarded (i.e. pay back user loans). The remainder will simply be burned without //@audit note that rewardProportion should be some number between 0-100%
     * being distributed to users. This undistributed tau cancels out bad debt in the vault. All vaults retain a growing reserve of yield to ensure bad debt
     * will always be covered.
     * _rewardProportion has a precision of 1e18. If _rewardProportion = 1e18, all tau will be disbursed to users. If _rewardProportion = 0, none of the burned tau will be disbursed.
     * @param _swapParams is the params to be passed to the SwapAdapter.
     * note that this function may only be called by a registered keeper.
     */
    function swapForTau(
        address _yieldTokenAddress,
        uint256 _yieldTokenAmount,
        uint256 _minTauReturned,
        bytes32 _swapAdapterHash,
        uint256 _rewardProportion,
        bytes calldata _swapParams
    ) external onlyKeeper whenNotPaused {
        // Ensure keeper is allowed to swap this token
        if (_yieldTokenAddress == collateralToken) {
            revert tokenCannotBeSwapped();
        }

        if (_yieldTokenAmount == 0) {
            revert zeroAmount();
        }

        // Get and validate swap adapter address
        address swapAdapterAddress = SwapAdapterRegistry(controller).swapAdapters(_swapAdapterHash);
        if (swapAdapterAddress == address(0)) {
            // The given hash has not yet been approved as a swap adapter.
            revert unregisteredSwapAdapter();
        }

        // Calculate portion of tokens which will be swapped for TAU and disbursed to the vault, and portion which will be sent to the protocol.
        uint256 protocolFees = (feeMapping[Constants.GLP_VAULT_PROTOCOL_FEE] * _yieldTokenAmount) /
            Constants.PERCENT_PRECISION;
        uint256 swapAmount = _yieldTokenAmount - protocolFees;

        // Transfer tokens to swap adapter
        IERC20(_yieldTokenAddress).safeTransfer(swapAdapterAddress, swapAmount);

        // Call swap function, which will transfer resulting tau back to this contract and return the amount transferred.
        // Note that this contract does not check that the swap adapter has transferred the correct amount of tau. This check
        // is handled by the swap adapter, and for this reason any registered swap adapter must be a completely trusted contract.
        uint256 tauReturned = BaseSwapAdapter(swapAdapterAddress).swap(tau, _swapParams);

        if (tauReturned < _minTauReturned) {
            revert tooMuchSlippage(tauReturned, _minTauReturned);
        }

        // Burn received Tau
        ERC20Burnable(tau).burn(tauReturned);

        // Add Tau rewards to withheldTAU to avert sandwich attacks
        _disburseTau();
        _withholdTau((tauReturned * _rewardProportion) / Constants.PERCENT_PRECISION); //@audit note that _rewardProportion is unvalidated -> a large value for this uint(256).max can ensure tha a large value of Tau is witheld -> not only enough to
        clear bad debts in the vault but ALL debts -> this effectively means that users can withdraw collateral without actual payments

        // Send protocol fees to FeeSplitter
        IERC20(_yieldTokenAddress).safeTransfer(
            Controller(controller).addressMapper(Constants.FEE_SPLITTER),
            protocolFees
        );

        // Emit event
        emit Swap(_yieldTokenAddress, protocolFees, swapAmount, tauReturned);
    }

    uint256[50] private __gap;
}
```

## Impact

All debt can be repaid and users can withdraw collateral without actually paying tokens

## Code

https://github.com/sherlock-audit/2023-03-taurus/blob/main/taurus-contracts/contracts/Vault/SwapHandler.sol
https://github.com/sherlock-audit/2023-03-taurus/blob/main/taurus-contracts/contracts/Vault/TauDripFeed.sol

## Recommendation

Validate `_rewardProportion` to have a max of 1e18 (100%)

## Links

https://code4rena.com/reports/2022-03-timeswap/#h-01-wrong-timing-of-check-allows-users-to-withdraw-collateral-without-paying-for-the-debt

## Learning

Unvalidated inputs can critically affect business logic -> this was a simple example that proves how a large number can disrupt the entire vault functionality and create a critical vulnerability. To see this impact, one needs to have a more intuitive understanding of protocol - what happens in edge cases with indirect side-effects.

Fuzzing could have discovered this quickly...Use fuzzing tools to find out inconsistent states
